component PMOS
  extends ConditionalHeatPort()
  D = Pin()
  G = Pin()
  S = Pin()
  B = Pin()
  # Width
  parameter W::Dyad.Length = 2.0e-5
  # Length
  parameter L::Dyad.Length = 6.0e-6
  # Transconductance parameter
  parameter Beta::Dyad.Transconductance = 1.05e-5
  # Zero bias threshold voltage
  parameter Vt::Dyad.Voltage = -1
  # Bulk threshold parameter
  parameter K2::Real = 0.41
  # Reduction of pinch-off region
  parameter K5::Real = 0.839
  # Narrowing of channel
  parameter dW::Dyad.Length = -2.5e-6
  # Shortening of channel
  parameter dL::Dyad.Length = -2.1e-6
  # Drain-Source-Resistance
  parameter RDS::Dyad.Resistance = 10000000
  # = true, if parameters Beta, K2 and Vt depend on temperature
  parameter useTemperatureDependency::Boolean = false
  # Parameter measurement temperature
  parameter Tnom::Dyad.Temperature = 300.15
  # Fitting parameter for Vt
  parameter kvt::Real = -0.0029
  # Fitting parameter for K2
  parameter kk2::Real = 0.00062
  variable v::Real
  variable uds::Real
  variable ubs::Real
  variable ugst::Real
  variable ud::Real
  variable us::Real
  variable id::Real
  variable gds::Real
  variable beta_t::Real
  variable vt_t::Real
  variable k2_t::Real
relations
  gds = (RDS < 1e-20 and RDS > -1e-20) ? 1e20 : 1 / RDS
  v = beta_t * (W + dW) / (L + dL)
  ud = identity(D.v > S.v ? S.v : D.v)
  us = identity(D.v > S.v ? D.v : S.v)
  uds = ud - us
  ubs = identity(B.v < us ? 0 : B.v - us)
  ugst = (G.v - us - vt_t + k2_t * ubs) * K5
  id = identity(ugst >= 0 ? uds * gds : (ugst < uds ? -v * uds * (ugst - uds / 2) + uds * gds : -v * ugst * ugst / 2 + uds * gds))
  beta_t = useTemperatureDependency ? Beta * (T_heatPort / Tnom) ^ (-1.5) : Beta
  vt_t = useTemperatureDependency ? Vt * (1 + (T_heatPort - Tnom) * kvt) : Vt
  k2_t = useTemperatureDependency ? K2 * (1 + (T_heatPort - Tnom) * kk2) : K2
  G.i = 0
  D.i = identity(D.v > S.v ? -id : id)
  S.i = identity(D.v > S.v ? id : -id)
  B.i = 0
  LossPower = D.i * (D.v - S.v)
end
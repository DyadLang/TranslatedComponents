partial component IdealSwitchWithArc
  extends OnePort
  extends ConditionalHeatPort(T = 293.15)
  # Closed switch resistance
  parameter Ron::Dyad.Resistance = 1.0e-5
  # Opened switch conductance
  parameter Goff::Dyad.Conductance = 1.0e-5
  # Initial arc voltage
  parameter V0::Dyad.Voltage = 30
  # Arc voltage slope
  parameter dVdt::Dyad.VoltageSlope = 10000
  # Max. arc voltage
  parameter Vmax::Dyad.Voltage = 60
  # Indicates off-state (but maybe not quenched)
  variable off::Boolean
  # Indicating quenched arc (if switch is off)
  variable quenched::Boolean
  # Last switch off time instant
  variable tSwitch::Dyad.Time
relations
  if edge(off)
    tSwitch = time
  end
  quenched = off and (abs(i) <= abs(v) * Goff or pre(quenched))
  if off
    if quenched
      i = Goff * v
    else
      v = min(Vmax, V0 + dVdt * (time - tSwitch)) * sign(i)
    end
  else
    v = Ron * i
  end
  LossPower = v * i
end